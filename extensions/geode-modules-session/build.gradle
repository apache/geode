import org.apache.geode.gradle.plugins.DependencyConstraints

/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'standard-subproject-configuration'
}

evaluationDependsOn(":geode-core")



dependencies {
  api(platform(project(':boms:geode-all-bom')))
  api(project(':extensions:geode-modules-session-internal')) {
    exclude module: 'geode-modules'
  }
  api(project(':geode-core'))
  implementation(project(':geode-common'))

  // Exclude logback from all configurations to avoid conflicts with log4j-slf4j-impl
  configurations.all {
    exclude group: 'ch.qos.logback'
    // Exclude the old log4j-slf4j-impl (for SLF4J 1.x) to avoid conflicts
    exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    // Exclude log4j-to-slf4j because we use log4j-slf4j2-impl (opposite direction)
    exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
  }

  integrationTestImplementation(project(':extensions:geode-modules'))
  integrationTestImplementation(project(':geode-dunit')) {
    exclude module: 'geode-core'
  }
  integrationTestImplementation(project(':geode-logging'))
  integrationTestImplementation(project(':geode-log4j'))

  // Add SLF4J 2.x to Log4j bridge for proper logging
  integrationTestImplementation('org.apache.logging.log4j:log4j-slf4j2-impl')

  implementation('jakarta.servlet:jakarta.servlet-api')
  implementation('org.apache.tomcat:servlet-api:' + DependencyConstraints.get('tomcat6.version'))
  implementation('org.slf4j:slf4j-api')

  // Spring Test 6.x provides Jakarta-compatible mock servlet objects
  integrationTestImplementation('org.springframework:spring-test')
  integrationTestImplementation('commons-io:commons-io')
  integrationTestImplementation('jakarta.servlet:jakarta.servlet-api')
  integrationTestImplementation('junit:junit')
  integrationTestImplementation('org.apache.tomcat:jasper:' + DependencyConstraints.get('tomcat6.version'))
  integrationTestImplementation('org.assertj:assertj-core')
  // Jetty 12: Servlet support moved to ee10 package for Jakarta EE 10
  integrationTestImplementation('org.eclipse.jetty:jetty-server')
  integrationTestImplementation('org.eclipse.jetty.ee10:jetty-ee10-servlet:' + DependencyConstraints.get('jetty.version'))
  integrationTestImplementation('org.eclipse.jetty:jetty-util')
  integrationTestImplementation('org.eclipse.jetty:jetty-http')
  integrationTestImplementation('org.httpunit:httpunit') {
    exclude group: 'jakarta.servlet'
    // this version of httpunit contains very outdated xercesImpl
    exclude group: 'xerces'
  }
  integrationTestImplementation('org.slf4j:slf4j-api')

  integrationTestRuntimeOnly('xerces:xercesImpl')
}

jar {
  manifest {
    attributes 'Main-Class': 'org.apache.geode.modules.session.installer.Installer'
  }
  archiveBaseName = 'geode-modules-session'
}

sonarqube {
  skipProject = true
}
