/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

evaluationDependsOn(":geode-core")

repositories {
  maven {
    // docker-compose-rule is published on bintray
    url 'https://dl.bintray.com/palantir/releases'
  }
}

// Run docker compose based tests here so that they don't get
// containerized by the acceptanceTest. These tests must run
// serially with respect to each other to avoid conflicts.
task acceptanceTestWithDockerCompose(type: Test) {
  testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
  classpath = sourceSets.acceptanceTest.runtimeClasspath
  outputs.upToDateWhen { false }

  useJUnit {
    includeCategories = ['org.apache.geode.test.junit.categories.DockerComposedTest']
  }

  forkEvery 1
  maxParallelForks 1
}

acceptanceTest.dependsOn(acceptanceTestWithDockerCompose)

acceptanceTest {
  useJUnit {
    excludeCategories += 'org.apache.geode.test.junit.categories.DockerComposedTest'
  }
}

dependencies {
  compile project(':geode-core')
  compile project(':geode-common')
  compile group: 'com.zaxxer', name: 'HikariCP', version: project.'HikariCP.version'


  testCompile project(':geode-junit')


  integrationTestCompile project(":geode-dunit")


  acceptanceTestCompile project(":geode-dunit")
  acceptanceTestCompile group: 'mysql', name: 'mysql-connector-java', version: '5.1.46'
  acceptanceTestCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-junit4', version: '0.31.1'

  acceptanceTestRuntime group: 'org.apache.derby', name: 'derby', version: project.'derby.version'
  acceptanceTestRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.2'
}

integrationTest.forkEvery 0