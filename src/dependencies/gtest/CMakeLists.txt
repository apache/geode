# Licensed to the Apache Software Foundation (ASF) under one or more
# contributor license agreements.  See the NOTICE file distributed with
# this work for additional information regarding copyright ownership.
# The ASF licenses this file to You under the Apache License, Version 2.0
# (the "License"); you may not use this file except in compliance with
# the License.  You may obtain a copy of the License at
# 
#      http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
cmake_minimum_required( VERSION 3.4 )
project( gtest )

set( ${PROJECT_NAME}_VERSION 1.7.0 )
set( ${PROJECT_NAME}_SHA265 b58cb7547a28b2c718d1e38aee18a3659c9e3ff52440297e965f5edffe34b6d0 )
set( ${PROJECT_NAME}_URL "https://github.com/google/googletest/archive/release-${${PROJECT_NAME}_VERSION}.zip" )
set( ${PROJECT_NAME}_EXTERN ${PROJECT_NAME}-extern )

include(ExternalProject)

if(CMAKE_CXX_COMPILER_ID STREQUAL "SunPro")
  set(SUN_COMPILER_FLAGS "-DCMAKE_CXX_FLAGS=-std=c++11 -m64")
endif()

ExternalProject_Add( ${${PROJECT_NAME}_EXTERN}
   URL ${${PROJECT_NAME}_URL}
   URL_HASH SHA256=${${PROJECT_NAME}_SHA265}
   UPDATE_COMMAND ""
   INSTALL_COMMAND ""
   CMAKE_ARGS "${SUN_COMPILER_FLAGS}" -Dgtest_force_shared_crt:BOOL=ON
)

ExternalProject_Get_Property( ${${PROJECT_NAME}_EXTERN} SOURCE_DIR )
set( ${PROJECT_NAME}_SOURCE_DIR ${SOURCE_DIR} )
ExternalProject_Get_Property( ${${PROJECT_NAME}_EXTERN} INSTALL_DIR )
set( ${PROJECT_NAME}_INSTALL_DIR ${INSTALL_DIR} )
ExternalProject_Get_Property( ${${PROJECT_NAME}_EXTERN} BINARY_DIR )
set( ${PROJECT_NAME}_BINARY_DIR ${BINARY_DIR}/${_DEBUG_OR_RELEASE} )
set( DEPENDENCIES_${PROJECT_NAME}_DIR ${${PROJECT_NAME}_BINARY_DIR} PARENT_SCOPE)

set( ${PROJECT_NAME}_STATIC_LIB
${${PROJECT_NAME}_BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${PROJECT_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}
${${PROJECT_NAME}_BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${PROJECT_NAME}_main${CMAKE_STATIC_LIBRARY_SUFFIX}
PARENT_SCOPE)

add_library(${PROJECT_NAME} INTERFACE)
target_include_directories(${PROJECT_NAME} INTERFACE
  $<BUILD_INTERFACE:${${PROJECT_NAME}_SOURCE_DIR}/include>
)
target_link_libraries(${PROJECT_NAME} INTERFACE
  ${${PROJECT_NAME}_BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${PROJECT_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}
)
add_dependencies(${PROJECT_NAME} ${${PROJECT_NAME}_EXTERN})

add_library(${PROJECT_NAME}_main INTERFACE)
target_include_directories(${PROJECT_NAME}_main INTERFACE
  $<BUILD_INTERFACE:${${PROJECT_NAME}_SOURCE_DIR}/include>
)
target_link_libraries(${PROJECT_NAME}_main INTERFACE
  ${${PROJECT_NAME}_BINARY_DIR}/${CMAKE_STATIC_LIBRARY_PREFIX}${PROJECT_NAME}_main${CMAKE_STATIC_LIBRARY_SUFFIX}
)
add_dependencies(${PROJECT_NAME}_main ${${PROJECT_NAME}_EXTERN})
