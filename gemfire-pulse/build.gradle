/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'war'

sourceSets {
  main {
    resources {
      exclude('**/gemfire*.properties')
      exclude('**/sqlfire.properties')
    }
  }
}

dependencies {
  compile 'commons-beanutils:commons-beanutils:'+project.'commons-beanutils.version'
  compile 'commons-collections:commons-collections:'+project.'commons-collections.version'
  compile 'commons-digester:commons-digester:'+project.'commons-digester.version'
  compile 'commons-lang:commons-lang:'+project.'commons-lang.version'
  compile 'org.springframework.ldap:spring-ldap-core:'+project.'spring-ldap-core.version'
  compile 'org.springframework.security:spring-security-config:'+project.'spring-security.version'
  compile 'org.springframework.security:spring-security-core:'+project.'spring-security.version'
  compile 'org.springframework.security:spring-security-ldap:'+project.'spring-security.version'
  compile 'org.springframework.security:spring-security-web:'+project.'spring-security.version'
  compile 'org.springframework:spring-tx:'+project.'spring-tx.version'

  providedCompile 'commons-logging:commons-logging:'+project.'commons-logging.version'

  provided 'org.mortbay.jetty:servlet-api:'+project.'mortbay-jetty-servlet-api.version'
  provided 'com.google.guava:guava:'+project.'guava.version'

  testCompile project(':gemfire-junit')

  testCompile 'org.apache.tomcat.embed:tomcat-embed-core:'+project.'tomcat7.version'
  testCompile 'org.apache.tomcat.embed:tomcat-embed-jasper:'+project.'tomcat7.version'
  testCompile 'org.apache.tomcat.embed:tomcat-embed-logging-juli:'+project.'tomcat7.version'
  testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:'+project.'selenium.version'
  testCompile 'org.seleniumhq.selenium:selenium-api:'+project.'selenium.version'
  testCompile 'org.seleniumhq.selenium:selenium-remote-driver:'+project.'selenium.version'
  testCompile 'org.seleniumhq.selenium:selenium-support:'+project.'selenium.version'

  testRuntime 'com.google.code.gson:gson:'+project.'google-gson.version'
  testRuntime 'org.apache.commons:commons-exec:'+project.'commons-exec.version'

}

def generatedResources = "$buildDir/generated-resources/main"

sourceSets {
  main {
    output.dir(generatedResources, builtBy: 'copyGemFireVersionFile')
  }
}

task copyGemFireVersionFile(type: Copy) {
  from project(':gemfire-core').createVersionPropertiesFile
  into generatedResources
}

jar {
  from sourceSets.main.output
}

eclipse.classpath.file {
  whenMerged { classpath ->
    classpath.entries.removeAll { entry -> entry.path.contains('gemfire-core/build')}
  }
}
idea.module.iml {
  whenMerged {module ->
    module.dependencies.removeAll { entry -> entry.toString().contains('gemfire-core/build')}
  }
}


artifacts {
  archives war
}

war {
  classpath configurations.runtime
  classpath project(':gemfire-core').webJar.archivePath
}

uiTest.dependsOn war
