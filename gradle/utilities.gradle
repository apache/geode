/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }
}

allprojects {
  //These methods will be added to each individual project.
  //Use this for methods that access project variables (like project.name)
  //
  //What is is doing is adding properties to the extra properties of the
  //project. Each property is itself a closure.
  ext {
    sanitizedName = {
      // Intended to strip off 'extensions/' from the modules so that artifacts
      // don't end up in libs/extensions/
      def parts = project.name.split("/")
      return parts[parts.length - 1];
    }

    disableMavenPublishing = {
      // Use this closure when a project should not publish anything to maven.
      afterEvaluate {
        tasks.withType(PublishToMavenLocal) {
          it.enabled = false
        }
        tasks.withType(PublishToMavenRepository) {
          it.enabled = false
        }
        tasks.withType(GenerateMavenPom) {
          it.enabled = false
        }
        tasks.withType(Sign) {
          it.enabled = false
        }
      }
    }
  }
}
