/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-library'
}

apply from: "${rootDir}/${scriptDir}/standard-subproject-configuration.gradle"
apply from: "${project.projectDir}/../gradle/publish.gradle"

dependencies {
  implementation(platform(project(':boms:geode-all-bom')))

  testImplementation('junit:junit')
  testImplementation(project(':geode-junit'))
  testImplementation('org.assertj:assertj-core')
  testImplementation('org.mockito:mockito-core')
  testImplementation('org.awaitility:awaitility')
}

ext.findJavac = { ->
  def javac = new File(System.getProperty('java.home') + '/bin/javac')
  if (javac.isFile() && javac.canExecute()) {
    return javac.toString()
  }

  javac = new File(System.getProperty('java.home') + '/../bin/javac')
  if (javac.isFile() && javac.canExecute()) {
    return javac.toString()
  }

  return 'javac'
}

compileJava {
  options.fork = true
  options.forkOptions.executable = project.findJavac()
  options.compilerArgs << '-XDignore.symbol.file'
}

compileTestJava {
  options.fork = true
  options.forkOptions.executable = project.findJavac()
  options.compilerArgs << '-XDignore.symbol.file'
}

ext.moduleName = group + '.unsafe'

jar {
  inputs.property("moduleName", moduleName)
  manifest {
    attributes('Automatic-Module-Name': moduleName)
  }
}
